cmake_minimum_required(VERSION 3.21)

project(goopax_examples LANGUAGES CXX)

set (CMAKE_CXX_STANDARD 20)

if (APPLE)
  enable_language(OBJCXX)
  set (CMAKE_OBJCXX_STANDARD 20)
  include(cmake/common.cmake)
endif()

# Disabling cuda examples by default because of compatibility issues between cuda and gcc>=13
option(WITH_CUDA "WITH_CUDA" OFF)

set(CMAKE_INSTALL_RPATH_USE_LINK_PATH on)

if (ANDROID OR IOS)
  message("Applying toolchain workaround, so that packages are found.")
  set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE BOTH)
  set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY BOTH)
  set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE BOTH)
endif()

include(ExternalProject)
include(Eigen.cmake)
include(SDL.cmake)
include(OpenCV.cmake)
if (NOT WIN32)
  include(Boost.cmake)
  include(GMP.cmake)
endif()

find_package(goopax REQUIRED)
find_package(Threads)
find_package(OpenCL)
find_package(Eigen3 3.3 NO_MODULE)
find_package(OpenCV)
find_package(SDL3)
find_package(Boost 1.65.0 COMPONENTS system)
find_library(gmp_LIBRARY NAMES libgmp.a gmp NO_CACHE)
find_path(gmp_INCLUDE NAMES gmp.h NO_CACHE)
if ("${gmp_LIBRARY}" STREQUAL "gmp_LIBRARY-NOTFOUND" OR
    "${gmp_INCLUDE}" STREQUAL "gmp_INCLUDE-NOTFOUND")
  if (TARGET gmp)
    ExternalProject_Get_property(gmp SOURCE_DIR)
    find_library(gmp_LIBRARY NAMES libgmp.a gmp HINTS ${SOURCE_DIR}/.libs NO_CACHE)
    find_path(gmp_INCLUDE NAMES gmp.h HINTS ${SOURCE_DIR} NO_CACHE)
  endif()
endif()

if (WITH_CUDA)
  find_package(CUDA)
  project(goopax_examples LANGUAGES CXX CUDA)
  set (CMAKE_CUDA_STANDARD 17)
endif()

macro(make_executable P)
  add_executable(${P} ${P}.cpp)
  target_link_libraries(${P} goopax::goopax)
  if (UNIX AND NOT APPLE AND NOT CYGWIN)
    target_link_libraries(${P} -ltbb)
  endif()
  if (IOS)
    set_apple_properties(${P})
  endif()
  install(TARGETS ${P} DESTINATION bin)
endmacro()

if (TARGET Eigen3::Eigen)
  add_subdirectory(common/draw)
endif()

if (GOOPAX_DRAW_WITH_OPENGL OR GOOPAX_DRAW_WITH_METAL AND NOT GOOPAX_DEBUG)
  make_executable(nbody)
  target_include_directories(nbody SYSTEM PRIVATE "common/draw/ext/glatter/include")
  target_link_libraries(nbody goopax_draw)
  if (APPLE)
    SET_SOURCE_FILES_PROPERTIES( nbody.cpp PROPERTIES LANGUAGE OBJCXX )
  endif()
endif()

if (TARGET OpenCV AND TARGET goopax_draw)
  make_executable(fft)
  target_link_libraries(fft INTERFACE ${OpenCV_LIBS})
  target_include_directories(fft SYSTEM INTERFACE ${OpenCV_INCLUDE_DIRS})
  target_link_libraries(fft goopax_draw)
  if (IOS)
    SET_TARGET_PROPERTIES(fft PROPERTIES MACOSX_BUNDLE_INFO_PLIST "${PROJECT_SOURCE_DIR}/ios/fft.plist")
  endif()
endif()

if (TARGET gmp AND TARGET Boost::system AND TARGET goopax_draw)
  make_executable(deep-zoom-mandelbrot)
  target_link_libraries(deep-zoom-mandelbrot INTERFACE ${gmp_LIBRARY})
  target_include_directories(deep-zoom-mandelbrot SYSTEM INTERFACE ${gmp_INCLUDE})
  target_link_libraries(deep-zoom-mandelbrot Boost::system goopax_draw)
endif()

if (TARGET goopax_draw)
  make_executable(mandelbrot)
  target_link_libraries(mandelbrot goopax_draw)
endif()

if (TARGET Eigen3::Eigen)
  make_executable(matmul)
  target_link_libraries(matmul Eigen3:Eigen)
endif()

if (TARGET OpenCL::OpenCL)
  make_executable(cl-interop-1)
  target_link_libraries(cl-interop-1 OpenCL::OpenCL)
  make_executable(cl-interop-2)
  target_link_libraries(cl-interop-2 OpenCL::OpenCL)
endif()

if (WITH_CUDA)
  add_executable(cuda-interop cuda-interop.cu)
  target_link_libraries(cuda-interop goopax::goopax)
  if (UNIX AND NOT APPLE AND NOT CYGWIN)
    target_link_libraries(cuda-interop -ltbb)
  endif()
endif()

make_executable(gather)
make_executable(svm-pingpong)
make_executable(memory-transfer)
make_executable(pi)
make_executable(simple)
make_executable(race-condition)
make_executable(helloworld)