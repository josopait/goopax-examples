cmake_minimum_required(VERSION 3.21)

project(goopax_examples LANGUAGES CXX)

set (CMAKE_CXX_STANDARD 20)

if (APPLE)
  enable_language(OBJCXX)
  set (CMAKE_OBJCXX_STANDARD 20)
  include(cmake/common.cmake)
endif()

# Disabling cuda examples by default because of compatibility issues between cuda and gcc>=13
option(WITH_CUDA "WITH_CUDA" OFF)

set(CMAKE_INSTALL_RPATH_USE_LINK_PATH on)

if (ANDROID OR IOS)
  message("Applying toolchain workaround, so that packages are found.")
  set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE BOTH)
  set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY BOTH)
  set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE BOTH)
endif()

include(ExternalProject)
include(Eigen.cmake)
include(SDL3.cmake)
include(OpenCV.cmake)
if (NOT WIN32)
  include(Boost.cmake)
  include(GMP.cmake)
endif()

find_package(goopax REQUIRED)
find_package(Threads)
find_package(OpenCL)
find_package(Eigen3 3.3 NO_MODULE)
find_package(OpenCV)
find_package(Boost 1.65.0 COMPONENTS system)
find_library(gmp_LIBRARY NAMES libgmp.a gmp NO_CACHE)
find_path(gmp_INCLUDE NAMES gmp.h NO_CACHE)
if ("${gmp_LIBRARY}" STREQUAL "gmp_LIBRARY-NOTFOUND" OR
    "${gmp_INCLUDE}" STREQUAL "gmp_INCLUDE-NOTFOUND")
  if (TARGET gmp_gpx)
    ExternalProject_Get_property(gmp_gpx SOURCE_DIR)
    find_library(gmp_LIBRARY NAMES libgmp.a gmp HINTS ${SOURCE_DIR}/.libs NO_CACHE)
    find_path(gmp_INCLUDE NAMES gmp.h HINTS ${SOURCE_DIR} NO_CACHE)
  endif()
endif()

if (WITH_CUDA)
  find_package(CUDA)
  project(goopax_examples LANGUAGES CXX CUDA)
  set (CMAKE_CUDA_STANDARD 17)
endif()

macro(make_goopax_exec P)
  add_executable(${P} ${P}.cpp)
  target_link_libraries(${P} PRIVATE goopax::goopax)
  if (UNIX AND NOT APPLE AND NOT CYGWIN)
    target_link_libraries(${P} PRIVATE -ltbb)
  endif()
  if (IOS)
    set_apple_properties(${P})
  endif()
  install(TARGETS ${P} DESTINATION bin)
endmacro()

add_subdirectory(common/draw)

if (GOOPAX_DRAW_WITH_OPENGL OR GOOPAX_DRAW_WITH_METAL AND NOT GOOPAX_DEBUG)
  make_goopax_exec(nbody)
  target_include_directories(nbody SYSTEM PRIVATE "common/draw/ext/glatter/include")
  target_link_libraries(nbody PRIVATE goopax_draw)
  if (APPLE)
    SET_SOURCE_FILES_PROPERTIES( nbody.cpp PROPERTIES LANGUAGE OBJCXX )
  endif()
endif()

if (OpenCV_FOUND AND TARGET goopax_draw)
  make_goopax_exec(fft)
  target_link_libraries(fft PRIVATE ${OpenCV_LIBS})
  target_include_directories(fft SYSTEM PRIVATE ${OpenCV_INCLUDE_DIRS})
  target_link_libraries(fft PRIVATE goopax_draw)
  if (IOS)
    SET_TARGET_PROPERTIES(fft PROPERTIES MACOSX_BUNDLE_INFO_PLIST "${PROJECT_SOURCE_DIR}/ios/fft.plist")
  endif()
endif()

if (NOT "${gmp_LIBRARY}" STREQUAL "gmp_LIBRARY-NOTFOUND" AND
    NOT "${gmp_INCLUDE}" STREQUAL "gmp_INCLUDE-NOTFOUND" AND
    Boost_FOUND AND
    TARGET goopax_draw)
  make_goopax_exec(deep-zoom-mandelbrot)
  target_link_libraries(deep-zoom-mandelbrot PRIVATE ${gmp_LIBRARY})
  target_include_directories(deep-zoom-mandelbrot SYSTEM PRIVATE ${gmp_INCLUDE})
  target_link_libraries(deep-zoom-mandelbrot PRIVATE Boost::system goopax_draw)
endif()

if (TARGET goopax_draw)
  make_goopax_exec(mandelbrot)
  target_link_libraries(mandelbrot PRIVATE goopax_draw)
endif()

if (Eigen3_FOUND)
  make_goopax_exec(matmul)
  target_link_libraries(matmul PRIVATE Eigen3::Eigen)
endif()

if (OpenCL_FOUND)
  make_goopax_exec(cl-interop-1)
  target_link_libraries(cl-interop-1 PRIVATE OpenCL::OpenCL)
  make_goopax_exec(cl-interop-2)
  target_link_libraries(cl-interop-2 PRIVATE OpenCL::OpenCL)
endif()

if (WITH_CUDA)
  add_executable(cuda-interop cuda-interop.cu)
  target_link_libraries(cuda-interop PRIVATE goopax::goopax)
  if (UNIX AND NOT APPLE AND NOT CYGWIN)
    target_link_libraries(cuda-interop PRIVATE -ltbb)
  endif()
endif()

make_goopax_exec(gather)
make_goopax_exec(svm-pingpong)
make_goopax_exec(memory-transfer)
make_goopax_exec(pi)
make_goopax_exec(simple)
make_goopax_exec(race-condition)
make_goopax_exec(helloworld)
