cmake_minimum_required(VERSION 3.15)

project(goopax_examples LANGUAGES CXX)
if (CMAKE_VERSION VERSION_LESS "3.20")
  set (CMAKE_CXX_STANDARD 20)
else()
  set (CMAKE_CXX_STANDARD 23)
endif()

include(cmake/macros.cmake)

# Disabling cuda examples by default because of compatibility issues between cuda and gcc>=13
option(WITH_CUDA "WITH_CUDA" OFF)
set(MY_CMAKE_ARGS "")
list(APPEND MY_CMAKE_ARGS "-DCMAKE_INSTALL_RPATH_USE_LINK_PATH:BOOL=on")
if (WITH_CUDA)
  find_package(CUDA)
  project(goopax_examples LANGUAGES CXX CUDA)
  list(APPEND MY_CMAKE_ARGS "-DCMAKE_CUDA_STANDARD:STRING=17")
endif()
if (WIN32)
  if (NOT CMAKE_CONFIGURATION_TYPES)
    set(CMAKE_CONFIGURATION_TYPES "Debug")
  endif()
  if (NOT CMAKE_MSVC_RUNTIME_LIBRARY)
    set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreadedDebug")
  endif()
  list(APPEND MY_CMAKE_ARGS "-DCMAKE_CONFIGURATION_TYPES:STRING=${CMAKE_CONFIGURATION_TYPES}" "-DCMAKE_MSVC_RUNTIME_LIBRARY:STRING=${CMAKE_MSVC_RUNTIME_LIBRARY}")
elseif (APPLE)
  enable_language(OBJCXX)
  list(APPEND MY_CMAKE_ARGS "-DCMAKE_OBJCXX_STANDARD:STRING=20")
  include(cmake/common.cmake)
endif()
if (ANDROID)
  list(APPEND MY_CMAKE_ARGS "-DCMAKE_SYSTEM_NAME:STRING=Android" "-DANDROID_ABI:STRING=$ENV{ABI}" "-DANDROID_PLATFORM:STRING=$ENV{platform_version_string}" "-DANDROID_NDK:STRING=$ENV{android_sdk}/ndk/$ENV{ndk_version}" "-DCMAKE_TOOLCHAIN_FILE:STRING=$ENV{android_sdk}/ndk/$ENV{ndk_version}/build/cmake/android.toolchain.cmake")
endif()
if (ANDROID OR IOS)
  message("Applying toolchain workaround, so that packages are found.")
  list(APPEND MY_CMAKE_ARGS "-DCMAKE_FIND_ROOT_PATH_MODE_PACKAGE:STRING=BOTH" "-DCMAKE_FIND_ROOT_PATH_MODE_LIBRARY:STRING=BOTH" "-DCMAKE_FIND_ROOT_PATH_MODE_INCLUDE:STRING=BOTH")
endif()
make_persistent("${MY_CMAKE_ARGS}")

find_package(goopax REQUIRED PATHS ../..)
find_package(Threads)
make_goopax_exec(gather)
make_goopax_exec(svm-pingpong)
make_goopax_exec(memory-transfer)
make_goopax_exec(pi)
make_goopax_exec(simple)
make_goopax_exec(race-condition)
make_goopax_exec(helloworld)

if (WITH_CUDA)
  add_executable(cuda-interop cuda-interop.cu)
  target_link_libraries(cuda-interop PRIVATE goopax::goopax)
  if (UNIX AND NOT APPLE AND NOT CYGWIN AND NOT ANDROID)
    target_link_libraries(cuda-interop PRIVATE -ltbb)
  endif()
endif()
find_package(OpenCL)
if (OpenCL_FOUND)
  make_goopax_exec(cl-interop-1)
  target_link_libraries(cl-interop-1 PRIVATE OpenCL::OpenCL)
  make_goopax_exec(cl-interop-2)
  target_link_libraries(cl-interop-2 PRIVATE OpenCL::OpenCL)
endif()

add_subdirectory(ext)

add_subdirectory(common/draw)

if (TARGET goopax_draw)
  make_goopax_exec(matmul)
  target_link_libraries(matmul PRIVATE goopax_draw)

  make_goopax_exec(mandelbrot)
  if (GOOPAX_DRAW_USE_SDL2)
    target_compile_definitions(mandelbrot PUBLIC -DUSE_SDL2=1)
  else()
    target_compile_definitions(mandelbrot PUBLIC -DUSE_SDL2=0)
  endif()
  target_link_libraries(mandelbrot PRIVATE goopax_draw)

  find_package(Boost 1.65.0 COMPONENTS system PATHS "${CMAKE_BINARY_DIR}/ext/boost")
  if (Boost_FOUND)
    make_goopax_exec(deep-zoom-mandelbrot)
    if (GOOPAX_DRAW_USE_SDL2)
      target_compile_definitions(deep-zoom-mandelbrot PUBLIC -DUSE_SDL2=1)
    else()
      target_compile_definitions(deep-zoom-mandelbrot PUBLIC -DUSE_SDL2=0)
    endif()
    target_link_libraries(deep-zoom-mandelbrot PRIVATE Boost::system goopax_draw)
  else()
    message(STATUS "deep-zoom-mandelbrot dependencies not found: Download with cmake --build build --target build_boost")
  endif()

  find_package(OpenCV PATHS "${CMAKE_BINARY_DIR}/ext/opencv")
  if (OpenCV_FOUND)
    make_goopax_exec(fft)
    if (GOOPAX_DRAW_USE_SDL2)
      target_compile_definitions(fft PUBLIC -DUSE_SDL2=1)
    else()
      target_compile_definitions(fft PUBLIC -DUSE_SDL2=0)
    endif()
    target_link_libraries(fft PRIVATE ${OpenCV_LIBS})
    target_include_directories(fft SYSTEM PRIVATE ${OpenCV_INCLUDE_DIRS})
    target_link_libraries(fft PRIVATE goopax_draw)
    if (IOS)
      SET_TARGET_PROPERTIES(fft PROPERTIES MACOSX_BUNDLE_INFO_PLIST "${PROJECT_SOURCE_DIR}/ios/fft.plist")
    endif()
  else()
    message(STATUS "fft dependencies not found: Download with cmake --build build --target build_opencv")
  endif()

  if (TARGET build_glatter)
    if (GOOPAX_DRAW_WITH_OPENGL OR GOOPAX_DRAW_WITH_METAL)
      if (NOT GOOPAX_DEBUG)
        make_goopax_exec(nbody)
        if (GOOPAX_DRAW_USE_SDL2)
          target_compile_definitions(nbody PUBLIC -DUSE_SDL2=1)
        else()
          target_compile_definitions(nbody PUBLIC -DUSE_SDL2=0)
        endif()
        ExternalProject_Get_Property(build_glatter SOURCE_DIR)
        target_include_directories(nbody PRIVATE ${SOURCE_DIR}/include)
        target_link_libraries(nbody PRIVATE goopax_draw)
        if (APPLE)
          SET_SOURCE_FILES_PROPERTIES( nbody.cpp PROPERTIES LANGUAGE OBJCXX )
        endif()
      else()
        message(STATUS "nbody not available if GOOPAX_DEBUG set")
      endif()
    else()
      message(STATUS "nbody not available for platform")
    endif()
  else()
    message(STATUS "nbody dependencies not found: Download with cmake --build build --target build_glatter")
  endif()
else()
  message(STATUS "goopax_draw not found.")
endif()
