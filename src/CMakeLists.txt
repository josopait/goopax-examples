cmake_minimum_required(VERSION 3.15)

project(goopax_examples LANGUAGES CXX)

set (CMAKE_CXX_STANDARD 20)

# Disabling cuda examples by default because of compatibility issues between cuda and gcc>=13
option(WITH_CUDA "WITH_CUDA" OFF)

set(MY_CMAKE_ARGS "")
if (WIN32)
  list(APPEND MY_CMAKE_ARGS "-DCMAKE_CONFIGURATION_TYPES:STRING=Debug" "-DCMAKE_MSVC_RUNTIME_LIBRARY:STRING=MultiThreadedDebug" "-DCMAKE_BUILD_TYPE:STRING=Debug" "-DOpenCV_STATIC:BOOL=ON")
elseif (APPLE)
  enable_language(OBJCXX)
  list(APPEND MY_CMAKE_ARGS "-DCMAKE_OBJCXX_STANDARD:STRING=20")
  include(cmake/common.cmake)
endif()
if (ANDROID OR IOS)
  message("Applying toolchain workaround, so that packages are found.")
  list(APPEND MY_CMAKE_ARGS "-DCMAKE_FIND_ROOT_PATH_MODE_PACKAGE:STRING=BOTH" "-DCMAKE_FIND_ROOT_PATH_MODE_LIBRARY:STRING=BOTH" "-DCMAKE_FIND_ROOT_PATH_MODE_INCLUDE:STRING=BOTH")
endif()
if (WITH_CUDA)
  find_package(CUDA)
  project(goopax_examples LANGUAGES CXX CUDA)
  list(APPEND MY_CMAKE_ARGS "-DCMAKE_CUDA_STANDARD:STRING=17")
endif()
set(APPEND MY_CMAKE_ARGS "-DCMAKE_INSTALL_RPATH_USE_LINK_PATH:BOOL=on")

macro(set_args A)
foreach(argument ${A})
  string(FIND "${argument}" ":" typepos)
  string(FIND "${argument}" "=" valuepos)
  if (typepos GREATER_EQUAL 1 AND valuepos GREATER_EQUAL 1)
  math(EXPR typepos "${typepos}-2")
  string(SUBSTRING "${argument}" 2 "${typepos}" argname)
  math(EXPR typepos "${typepos}+3")
  math(EXPR valueoffset "${valuepos}-${typepos}")
  string(SUBSTRING "${argument}" "${typepos}" "${valueoffset}" argtype)
  math(EXPR valuepos "${valuepos}+1")
  string(SUBSTRING "${argument}" "${valuepos}" -1 argvalue)
  set("${argname}" "${argvalue}" CACHE "${argtype}" "")
  message("${argname} is ${argvalue} of type ${argtype}")
  endif()
endforeach()
endmacro()

set_args("${MY_CMAKE_ARGS}")

find_package(goopax REQUIRED PATHS ../..)
find_package(Threads)
find_package(OpenCL)
find_package(Boost 1.65.0 COMPONENTS system PATHS "${CMAKE_BINARY_DIR}/ext/boost")
find_package(SDL3 PATHS "${CMAKE_BINARY_DIR}/ext/sdl3")
find_package(Eigen3 3.3 NO_MODULE PATHS "${CMAKE_BINARY_DIR}/ext/eigen")

find_package(OpenCV PATHS "${CMAKE_BINARY_DIR}/ext/opencv")
if (OpenCV_FOUND)
  add_library(opencv INTERFACE)
  target_link_libraries(opencv INTERFACE ${OpenCV_LIBS})
  target_include_directories(opencv SYSTEM INTERFACE ${OpenCV_INCLUDE_DIRS})
endif()

add_subdirectory(ext)

add_subdirectory(common/draw)


macro(add_withfile P FN)
  set(T ${ARGV})
  list(REMOVE_AT T 0 1)
  set(missing "")
  foreach(L ${T})
    if (NOT TARGET ${L})
      list(APPEND missing "${L}")
    endif()
  endforeach()
  if ("${missing}" STREQUAL "")
    message("building target ${P}")
    add_executable(${P} ${FN})
    install(TARGETS ${P} DESTINATION bin)

    foreach(L ${T})
      target_link_libraries(${P} ${L})
    endforeach()
    target_link_libraries(${P} goopax::goopax)
    if (UNIX AND NOT APPLE AND NOT CYGWIN)
      target_link_libraries(${P} -ltbb)
    endif()
    if (IOS)
      set_apple_properties(${P})
    endif()
  else()
    message("not building target ${P} [missing: ${missing}]")
  endif()
endmacro()

macro(add P)
  set(T ${ARGV})
  list(REMOVE_AT T 0)
  add_withfile(${P} ${P}.cpp ${T})
endmacro()

if (GOOPAX_DRAW_WITH_OPENGL OR GOOPAX_DRAW_WITH_METAL AND NOT GOOPAX_DEBUG)
  add(nbody goopax_draw)
  if (TARGET nbody)
    target_include_directories(nbody SYSTEM PRIVATE "common/draw/ext/glatter/include")
  endif()
  if (APPLE)
    SET_SOURCE_FILES_PROPERTIES( nbody.cpp PROPERTIES LANGUAGE OBJCXX )
  endif()
endif()


add(deep-zoom-mandelbrot Boost::system goopax_draw)
add(gather)
add(mandelbrot goopax_draw)
add(fft opencv goopax_draw)
add(svm-pingpong)
add(memory-transfer)
add(pi)
add(simple)
add(race-condition)
add(helloworld)
add(matmul Eigen3::Eigen)
add(cl-interop-1 OpenCL::OpenCL)
add(cl-interop-2 OpenCL::OpenCL)

if (IOS AND TARGET fft)
  SET_TARGET_PROPERTIES(fft PROPERTIES MACOSX_BUNDLE_INFO_PLIST "${PROJECT_SOURCE_DIR}/ios/fft.plist")
endif()

if (WITH_CUDA)
  add_executable(cuda-interop cuda-interop.cu)
  target_link_libraries(cuda-interop goopax::goopax)
  if (UNIX AND NOT APPLE AND NOT CYGWIN)
    target_link_libraries(cuda-interop -ltbb)
  endif()
endif()
